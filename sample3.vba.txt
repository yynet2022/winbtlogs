Option VBASupport 1
Option Explicit
Option Base 1

REM  *****  BASIC  *****

Const i日付 As Integer = 1
Const i勤務開始 As Integer = 2
Const i勤務終了 As Integer = 3
Const i勤務日種別 As Integer = 4
Const i休憩時間 As Integer = 5
Const i実労働時間 As Integer = 6
Const i残業時間正 As Integer = 7
Const i残業時間負 As Integer = 8
Const i勤務中断 As Integer = 9

Const t勤務 As Integer = 0
Const tAM休 As Integer = 1
Const tPM休 As Integer = 2
Const t終日休 As Integer = 3
Const t休日 As Integer = 10

Const i開始行 As Integer = 12

Sub セットアップ()
	On Error GoTo myError
	Dim 年 As Integer, 月 As Integer
	年 = Cells(i開始行-2,2)
	月 = Cells(i開始行-2,3)

	Dim dateS As Date: dateS = DateSerial (年, 月, 1)
	Dim dateE As Date: dateE = DateSerial (年, 月+1, 0)
	Dim idate As Date
	Dim k As Integer: k = i開始行
	Dim j As Integer
	For idate = dateS To dateE
		With Cells(k, i日付)
			.Value = idate
			.NumberFormatLocal = "m月d日"
		End With
		If Weekday(idate) = vbSunday Or Weekday(idate) = vbSaturday Then
			Cells (k, i勤務日種別) = t休日
		ElseIf Cells (k, i勤務日種別) = t休日 Then
			Cells (k, i勤務日種別).ClearContents
		End If
		Range(Cells(k, i勤務開始), Cells(k, i勤務終了)).NumberFormatLocal = "[h]:mm"
		With Range(Cells(k, i休憩時間), Cells(k, i残業時間負))
			.Value = 0
			.NumberFormatLocal = "[h]:mm"
			.ClearContents
		End With
		For j = i勤務中断 To i勤務中断+30 Step 3
			Cells(k, j).NumberFormatLocal = "[h]:mm"
			Cells(k, j+1).NumberFormatLocal = "[h]:mm"
			Cells(k, j+2).NumberFormatLocal = "@"
			Cells(k, j+2).HorizontalAlignment = xlRight
		Next j
		k = k + 1
	Next idate

	Range(Cells(k, 1), Cells(k, 30)).ClearContents
	Cells(k, i日付).Value = "合計"
	Range(Cells(k, i休憩時間), Cells(k, i残業時間負)).NumberFormatLocal = "[h]:mm"
	k = k + 1

	For j = k To k + 5
		Range(Cells(j, 1), Cells(j, 30)).ClearContents
	Next j

	myError:
	If Err.Number <> 0 Then
		MsgBox "エラーが発生しました。" & vbCrLf & _
			"エラー番号：" & Err.Number & vbCrLf & _
			"エラー概要：" & Err.Description, vbExclamation
	End If
End Sub

Sub メイン()
	On Error GoTo myError
	Dim BASETIME As Date: BASETIME = TimeSerial (5, 0, 0)

	Const j開始 As Integer = 2
	Const j終了 As Integer = 3

	Dim 定時(2) As Date
	定時(1) = Cells(2, j開始) - BASETIME
	定時(2) = Cells(2, j終了) - BASETIME

	Dim 昼休み(2) As Date
	昼休み(1) = Cells(3, j開始) - BASETIME
	昼休み(2) = Cells(3, j終了) - BASETIME

	Dim 朝休み(2) As Date
	朝休み(1) = Cells(4, j開始) - BASETIME
	朝休み(2) = Cells(4, j終了) - BASETIME

	Dim 夕休み(2) As Date
	夕休み(1) = Cells(5, j開始) - BASETIME
	夕休み(2) = Cells(5, j終了) - BASETIME

	Dim 夕休みAM(2) As Date
	夕休みAM(1) = Cells(6, j開始) - BASETIME
	夕休みAM(2) = Cells(6, j終了) - BASETIME

	Dim 深夜休み(2) As Date
	深夜休み(1) = Cells(7, j開始) - BASETIME
	深夜休み(2) = Cells(7, j終了) - BASETIME

	Dim 未明休み(2) As Date
	未明休み(1) = Cells(8, j開始) - BASETIME
	未明休み(2) = Cells(8, j終了) - BASETIME

	Dim 勤務中断(2) As Date

	Dim 年 As Integer, 月 As Integer
	年 = Cells(i開始行-2,2)
	月 = Cells(i開始行-2,3)

	Dim j As Integer
	Dim v As Variant, u As Variant

	Dim dateS As Date: dateS = DateSerial (年, 月, 1)
	Dim dateE As Date: dateE = DateSerial (年, 月+1, 0)
	Dim idate As Date
	Dim k As Integer: k = i開始行
	For idate = dateS To dateE
		' Cells(k, i日付) = idate

		Dim 勤務日種別 As Integer
		勤務日種別 = Cells (k, i勤務日種別)
		If 勤務日種別 = t終日休 Then
			Cells(k, i勤務開始).ClearContents
			Cells(k, i勤務終了).ClearContents
		End If

		If IsEmpty(Cells(k, i勤務開始).Value) Or IsEmpty(Cells(k, i勤務終了).Value) Then
			With Range(Cells(k, i休憩時間), Cells(k, i残業時間負))
				.Value = 0
				.ClearContents
			End With
		Else
			Dim 勤務開始 As Date, 勤務終了 As Date
			勤務開始 = Cells (k, i勤務開始) - BASETIME
			勤務終了 = Cells (k, i勤務終了) - BASETIME

			Dim 休憩時間 As Date
			休憩時間 = 0

			Dim 定時時間 As Date
			定時時間 = 0

			u = 0
			If 勤務日種別 = t休日 Then
				u = Array()
			ElseIf 勤務日種別 = tAM休 Then
				定時時間 = 定時(2) - 昼休み(2)

				v = Array(TimeValue("00:00"), 昼休み(2))
				休憩時間 = 休憩時間 + 重複時間(勤務開始, 勤務終了, CDate(v(1)), CDate(v(2))) _
								   + 重複時間(勤務開始, 勤務終了, 夕休みAM(1), 夕休みAM(2)) _
								   + 重複時間(勤務開始, 勤務終了, 深夜休み(1), 深夜休み(2)) _
								   + 重複時間(勤務開始, 勤務終了, 未明休み(1), 未明休み(2))

				u = Array(v, 夕休みAM, 深夜休み, 未明休み)
			ElseIf 勤務日種別 = tPM休 Then
				定時時間 = 昼休み(1) - 定時(1)

				v = Array(昼休み(1), CDate(2))
				休憩時間 = 休憩時間 + 重複時間(勤務開始, 勤務終了, 朝休み(1), 朝休み(2)) _
								   + 重複時間(勤務開始, 勤務終了, CDate(v(1)), CDate(v(2)))

				u = Array(朝休み, v)
			Else
				定時時間 = (定時(2) - 定時(1)) - (昼休み(2) - 昼休み(1))

				休憩時間 = 休憩時間 + 重複時間(勤務開始, 勤務終了, 朝休み(1), 朝休み(2)) _
								   + 重複時間(勤務開始, 勤務終了, 昼休み(1), 昼休み(2)) _
								   + 重複時間(勤務開始, 勤務終了, 夕休み(1), 夕休み(2)) _
								   + 重複時間(勤務開始, 勤務終了, 深夜休み(1), 深夜休み(2)) _
								   + 重複時間(勤務開始, 勤務終了, 未明休み(1), 未明休み(2))

				u = Array(朝休み, 昼休み, 夕休み, 深夜休み, 未明休み)
			End If

			j = i勤務中断
			Do Until IsEmpty(Cells(k, j).Value) Or IsEmpty(Cells(k, j+1).Value)
				Dim ks As Date: ks = Cells(k, j)
				Dim ke As Date: ke = Cells(k, j+1)
				if Not IsEmpty(Cells(k, j+2).Value) Then
					If ks < ke Then
						定時時間 = 定時時間 - (ke - ks)
					End If
				End If
				勤務中断(1) = p_Max(勤務開始, ks - BASETIME)
				勤務中断(2) = p_Min(勤務終了, ke - BASETIME)
				休憩時間 = 休憩時間 + 中断時間(勤務中断(), u)
				j = j + 3
			Loop

			Dim 実労働時間 As Date: 実労働時間 = 0
			If 勤務終了 > 勤務開始 + 休憩時間 Then
				実労働時間 = 勤務終了 - 勤務開始 - 休憩時間
			End If
			Cells(k, i休憩時間).Value = 休憩時間
			Cells(k, i実労働時間).Value = 実労働時間
			If 実労働時間 < 定時時間 Then
				Cells(k, i残業時間正).ClearContents
				Cells(k, i残業時間負).Value = 定時時間 - 実労働時間
			Else
				Cells(k, i残業時間正).Value = 実労働時間 - 定時時間
				Cells(k, i残業時間負).ClearContents
			End If
		End If
		k = k + 1
	Next idate

	For j = i休憩時間 To i残業時間負
		Cells(k, j) = WorksheetFunction.Sum(Range(Cells(i開始行,j), Cells(k-1,j)))
	Next j

	myError:
	If Err.Number <> 0 Then
		Dim s As String: s = ""
		If Not IsEmpty(idate) Then s = "(at " & idate & ")"
		MsgBox "エラーが発生しました。" & s & vbCrLf & _
			"エラー番号：" & Err.Number & vbCrLf & _
			"エラー概要：" & Err.Description, vbExclamation
	End If
End Sub

Private Function 中断時間(中断() As Date, 休憩リスト As Variant) As Date
	Dim t As Date: t = 0

	Dim v As Variant
	For Each v In 休憩リスト
		t = t + 重複時間(中断(1), 中断(2), CDate(v(1)), CDate(v(2)))
	Next v

	中断時間 = 0
	If 中断(2) > 中断(1) + t Then 中断時間 = 中断(2) - 中断(1) - t
End Function

Private Function 重複時間(甲開始 As Date, 甲終了 As Date, 乙開始 As Date, 乙終了 As Date) As Date
	Dim v As Date: v = p_Max(甲開始, 乙開始)
	Dim u As Date: u = p_Min(甲終了, 乙終了)
	重複時間 = 0
	If v < u Then 重複時間 = u - v
End Function

Private Function p_Max(a,b)
	p_Max = a
	If b > a Then p_Max = b
End Function

Private Function p_Min(a,b)
	p_Min = a
	If b < a Then p_Min = b
End Function
